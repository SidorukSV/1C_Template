
#Область Переменные

&НаКлиенте
Перем ВыполняетсяЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройки();
	ПолучитьСписокВыбораПоиска();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВыполняетсяЗакрытие = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие И ЭтаФорма.Модифицированность Тогда
		Отказ = Истина;
		ОповещениеВопрос = Новый ОписаниеОповещения("СохранитьНастройкиПослеВопроса", ЭтаФорма);
		ПоказатьВопрос(ОповещениеВопрос, "Имеются несохраненные данные, выполнить сохранение настроек?", РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПриИзменении(Элемент)
	Если НЕ ПустаяСтрока(Поиск) Тогда
		Элемент = Элементы.Найти(Поиск);
		Если Элемент <> Неопределено Тогда
			ЭтаФорма.ТекущийЭлемент = Элемент;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СохранитьНастройки();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписок(Команда)
	
	ОткрытьФорму("Справочник.бит_НастройкиУчета.ФормаСписка", Новый Структура("ОткрытиеФормыСписка"), , Новый УникальныйИдентификатор);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНастройки()
	
	ПростыеНастройки = ПолучитьСписокПростыхНастроек();
	Для Каждого ПростаяНастройка Из ПростыеНастройки Цикл
		Настройка = Справочники.бит_НастройкиУчета[ПростаяНастройка.ИмяНастройки]; 
		Если ЗначениеЗаполнено(ПростаяНастройка.ИмяКлюча) Тогда 
			ЭтаФорма[ПростаяНастройка.ИмяКлюча] = Настройка.Ключ;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПростаяНастройка.ИмяЗначения) Тогда 
			ЭтаФорма[ПростаяНастройка.ИмяЗначения] = Настройка.Значение;
		КонецЕсли;
	КонецЦикла;
	
	ТабличныеНастройки = ПолучитьСписокТабличныхНастроек();
	Для Каждого ТабличнаяНастройка Из ТабличныеНастройки Цикл
		ЗагрузитьТабличнуюНастройку(ТабличнаяНастройка.ИмяНастройки, ТабличнаяНастройка.ИмяТаблицы);
	КонецЦикла;
			
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	НачатьТранзакцию();
	
	ПростыеНастройки = ПолучитьСписокПростыхНастроек();
	Для Каждого ПростаяНастройка Из ПростыеНастройки Цикл
		НастройкаОбъект = Справочники.бит_НастройкиУчета[ПростаяНастройка.ИмяНастройки].ПолучитьОбъект();
		Если ЗначениеЗаполнено(ПростаяНастройка.ИмяКлюча) Тогда 
			НастройкаОбъект.Ключ = ЭтаФорма[ПростаяНастройка.ИмяКлюча];
		КонецЕсли;
		Если ЗначениеЗаполнено(ПростаяНастройка.ИмяЗначения) Тогда 
			НастройкаОбъект.Значение = ЭтаФорма[ПростаяНастройка.ИмяЗначения];
		КонецЕсли;
		НастройкаОбъект.Записать();
	КонецЦикла;
	
	ТабличныеНастройки = ПолучитьСписокТабличныхНастроек();
	Для Каждого ТабличнаяНастройка Из ТабличныеНастройки Цикл
		СохранитьТабличнуюНастройку(ТабличнаяНастройка.ИмяНастройки, ТабличнаяНастройка.ИмяТаблицы);
	КонецЦикла;
			
	ЗафиксироватьТранзакцию();
	
	ЭтаФорма.Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиПослеВопроса(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.Модифицированность = Ложь;
	ВыполняетсяЗакрытие = Истина;
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьНастройки();
		Закрыть();
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокПростыхНастроек()
	
	ПростыеНастройки = Новый Массив;
	//ПРИМЕР КАК ДОБАВЛЯТЬ НАСТРОЙКИ:
	//1. Добавляем предопределенный элемент МояНастройка
	//2. Добавляем реквизит на форму с нужным типом данных
	//3. Вставляем строчку в эту функцию в таком формате:
	//	ПростыеНастройки.Добавить(Новый Структура("ИмяНастройки, ИмяКлюча, ИмяЗначения", "МояНастройка",, "МойРеквизитФормы"));
	// или ПростыеНастройки.Добавить(Новый Структура("ИмяНастройки, ИмяКлюча, ИмяЗначения", "МояНастройка", "МойРеквизитФормыКлюч", "МойРеквизитФормы"));
	// если для настройки требуются 2 реквизита, один из них ключ, а другой значение (редко используется)
	
	Возврат ПростыеНастройки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокТабличныхНастроек()
	
	ТабличныеНастройки = Новый Массив;
	//ПРИМЕР КАК ДОБАВЛЯТЬ НАСТРОЙКИ:
	//1. Добавляем предопределенный элемент МояНастройка
	//2. Добавляем реквизит на форму с типом ТаблицаЗначений, 
	//	с колонками Ключ и Значение нужных типов (допускается, только Ключ или только Значение)
	//3. Вставляем строчку в эту функцию в таком формате:
	//ТаблицыСоответствия.Добавить(Новый Структура("ИмяНастройки, ИмяТаблицы", "МояНастройка", "РеквизитФормыТаблицаЗначений"));
	
	Возврат ТабличныеНастройки;
  
КонецФункции

&НаСервере
Процедура ЗагрузитьТабличнуюНастройку(ИмяНастройки, ИмяТаблицы)
	
	НастройкаОбъект = Справочники.бит_НастройкиУчета[ИмяНастройки].ПолучитьОбъект();
	ТаблицаСоответствия = НастройкаОбъект.Значения;
	ТаблицаФормы = ЭтаФорма[ИмяТаблицы];
	
	Для Каждого Строка Из ТаблицаСоответствия Цикл
		СтрокаФормы = ТаблицаФормы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаФормы, Строка);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура СохранитьТабличнуюНастройку(ИмяНастройки, ИмяТаблицы)
	
	НастройкаОбъект = Справочники.бит_НастройкиУчета[ИмяНастройки].ПолучитьОбъект();
	ТаблицаФормы = ЭтаФорма[ИмяТаблицы];
	НастройкаОбъект.Значения.Очистить();
	
	Для Каждого Строка Из ТаблицаФормы Цикл
		СтрокаНастройки = НастройкаОбъект.Значения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНастройки, Строка);
	КонецЦикла; 
	
	НастройкаОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСписокВыбораПоиска()
	
	Массив = ЭтаФорма.ПолучитьРеквизиты();
	Соответствие = Новый Соответствие;
	
	Для Каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") И ЗначениеЗаполнено(Элемент.ПутьКДанным) И Элемент <> Элементы._Поиск Тогда
			Для Каждого Реквизит Из Массив Цикл
				Если Элемент.ПутьКДанным = Реквизит.Имя Тогда
					Соответствие.Вставить(Реквизит, Элемент);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого соотЭлемент Из Соответствие Цикл
		Если ТипЗнч(соотЭлемент.Значение) = Тип("ПолеФормы") Тогда
			Если ЗначениеЗаполнено(соотЭлемент.Значение.ПутьКДанным) Тогда
				Элементы._Поиск.СписокВыбора.Добавить(соотЭлемент.Значение.Имя, ?(НЕ ПустаяСтрока(соотЭлемент.Значение.Заголовок), соотЭлемент.Значение.Заголовок, соотЭлемент.Ключ.Заголовок));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Элементы._Поиск.СписокВыбора.СортироватьПоПредставлению();

КонецПроцедуры

#КонецОбласти