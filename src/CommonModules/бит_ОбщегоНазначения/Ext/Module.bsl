// Процедура - Установить условное оформление
//
// Параметры:
//  Реквизит - ДинамическийСписок, ФормаКлиентскогоПриложения, НастройкиКомпоновкиДанных, Произвольный -
//				Объект оформления, любой объет, у которого есть свойство УсловноеОформление 
//  ЛЗ - Строка - поле отбора.
//  ПЗ - Произвольный - значение отбора
//  СтруктураПараметров	- Структура - информация об оформлении:
//    * ЦветФона - Цвет
//    * ЦветТекста - Цвет
//    * Шрифт - Шрифт
//    * ГоризонтальноеПоложение - ГоризонтальноеПоложение 
//    * Формат - Строка 
//    * ВыделятьОтрицательные - Булево
//    * ОтметкаНезаполненного - Булево 
//    * Текст - Строка 
//    * Видимость - Булево 
//    * Доступность - Булево 
//    * ТолькоПросмотр - Булево 
//    * Отображать - Булево 
//  ВидСравнения - ВидСравненияКомпоновкиДанных	- вид сравнения, по умолчанию ВидСравненияКомпоновкиДанных.Равно 
//  Поля - Массив, Строка - путь к полям (полю) компоновки данных
//  Представление - Строка - представление оформления 
//
Процедура УстановитьУсловноеОформление(Реквизит, ЛЗ, ПЗ = Неопределено, СтруктураПараметров, ВидСравнения = Неопределено, Поля = Неопределено, Представление = "") Экспорт
  
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	ЭлементОформления = Реквизит.УсловноеОформление.Элементы.Добавить();
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	Если ТипЗнч(Поля) = Тип("Массив") Тогда
		Для Каждого Поле Из Поля Цикл
			ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
			ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Поле);
		КонецЦикла;                                                
	ИначеЕсли ТипЗнч(Поля) = Тип("Строка") Тогда
		ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
		ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Поля);
	КонецЕсли; 
	
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ЛЗ); 
	ЭлементОтбора.ВидСравнения = ВидСравнения; 

	Если ПЗ <> Неопределено Тогда
		ЭлементОтбора.ПравоеЗначение = ПЗ;
	КонецЕсли;    
	
	Для Каждого КиЗ Из СтруктураПараметров Цикл
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла; 
	
	ЭлементОформления.Представление = Представление; 
	
КонецПроцедуры
